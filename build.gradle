plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.5.3"
    id "com.diffplug.spotless" version "6.11.0"
    // note: We cannot use the latest version because of the following error.
    // > org/eclipse/jgit/lib/Repository has been compiled by a more recent version of the Java Runtime (class file version 55.0), this version of the Java Runtime only recognizes class file versions up to 52.0
    // id "com.palantir.git-version" version "0.15.0"
    id "com.palantir.git-version" version "0.12.3"
}
repositories {
    mavenCentral()
}
group = "pro.civitaspo"
description = "An Embulk plugin to ingest data from REST API (Content-Type=application/json) with jq."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.36"
    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"

    compileOnly "org.embulk:embulk-util-config:0.3.1"
    compileOnly "org.embulk:embulk-util-json:0.1.1"
    compileOnly "org.embulk:embulk-util-timestamp:0.2.1"
    compileOnly "org.hibernate:hibernate-validator:8.0.0.Final"
    compileOnly "org.glassfish:jakarta.el:4.0.2"

    compileOnly "org.embulk:embulk-base-restclient:0.10.1"
    compileOnly("org.embulk:embulk-util-retryhelper-jaxrs:0.8.1") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    // NOTE: Avoid 'java.lang.LinkageError: ClassCastException: attempting to castjar:file:./build/gemContents/classpath/javax.ws.rs-api-2.0.1.jar!/javax/ws/rs/client/ClientBuilder.class to jar:file:./build/gemContents/classpath/javax.ws.rs-api-2.0.1.jar!/javax/ws/rs/client/ClientBuilder.class'
    // compile "org.glassfish.jersey.core:jersey-client:3.0.3"
    compileOnly "org.glassfish.jersey.core:jersey-client:2.25.1"
    compileOnly project(path: ":shadow-jackson-jq", configuration: 'shadow')

    testImplementation platform('org.junit:junit-bom:5.8.1')
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.embulk:embulk-core:${embulkVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkVersion}"
    testImplementation "org.embulk:embulk-input-config:${embulkVersion}"
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.32.0'
}
embulkPlugin {
    mainClass = "pro.civitaspo.embulk.input.http_json.HttpJsonInputPlugin"
    category = "input"
    type = "http_json"
}
publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}
gem {
    from("LICENSE.txt")
    authors = [ "Civitaspo" ]
    email = [ "civitaspo@gmail.com" ]
    summary = "An Embulk plugin to ingest data from REST API (Content-Type=application/json) with jq."
    homepage = "https://github.com/civitaspo/embulk-input-http_json"
    licenses = [ "MIT" ]
}
spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat().aosp()
    }
}
test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams true
    }
}
